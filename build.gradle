apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'maven'
apply plugin: 'signing'

defaultTasks 'jar'

/** Helper function to run a command. Returns the command output if the command succeeded. */
def tryCommand(List command, boolean failOnError = false) {
	def out = new StringBuilder()
	def err = new StringBuilder()
	def proc = command.execute()
	proc.waitForProcessOutput(out, err)
	if (proc.exitValue()) {
		def msg = "failed to run '${command[0]}' command (args: ${command.tail()})\n$err$out"
		if (failOnError) {
			throw new GradleException(msg)
		} else {
			print 'Warning: '
			println msg
		}
		""
	} else {
		if (err) println "${err}"
		out.toString()
	}
}

/** Helper function to retrieve the current version string. */
def getVersion() {
	if (hasProperty('newVersion')) {
		// A new version string should be saved.
		def date = new Date()
		Properties version = new Properties()
		version['version'] = newVersion
		version['timestamp'] = date.format("yyyy-MM-dd'T'HH:mm'Z'")
		version['build.date'] = date.format('yyyy-MM-dd')
		version.store(file('src/res/Version.properties').newWriter(), null)
		return newVersion
	} else {
		def output = tryCommand(['git', 'describe'])
		if (output) {
			// Use Git describe output.
			output.readLines()[0]
		} else {
			// Git command failed. Read the previous version string instead.
			Properties version = new Properties()
			version.load(new FileInputStream(file('src/res/Version.properties')))
			version['version']
		}
	}
}

project.version = getVersion()
project.group = 'org.jastadd'
project.archivesBaseName = 'jastadd'

println("Building version: ${project.version}")

sourceCompatibility = '1.6'
targetCompatibility = '1.6'

mainClassName = 'org.jastadd.JastAdd'
jar.manifest.attributes 'Main-Class': mainClassName
jar.destinationDir = projectDir
jar.archiveName = 'jastadd2.jar'

configurations {
	deployerJars
	jastadd
	javacc
}

repositories {
	mavenCentral()
	mavenLocal()
}

dependencies {
	compile 'org.apache.ant:ant:1.9.4'
	jastadd('org.jastadd:jastadd:2.1.13') { force = true /* avoid conflict with local artifact */ }
	javacc 'net.java.dev.javacc:javacc:4.0'
}

processResources.dependsOn 'updateVersionString'
processResources.dependsOn 'copyResources'

task copyResources(type: Copy) {
	from 'src/template'
	into 'src/gen-res/template'
}

task release(dependsOn: [ 'sourceZip', 'binaryZip' ]) << {
	if (!project.hasProperty('newVersion')) {
		throw new GradleException('You must set -PnewVersion=<VERSION> when running the release task!')
	}
	assert version.equals(newVersion)
	def versionFile = file('src/res/Version.properties')
	tryCommand(['git', 'add', versionFile.path], true)
	tryCommand(['git', 'commit', '-m', "Release $version"], true)
	tryCommand(['git', 'tag', '-a', "$version", '-m', "Version $version"], true)
}

task sourceZip(type: Zip, dependsOn: 'documentation') {
	destinationDir = project.rootDir
	archiveName = 'jastadd2-src.zip'

	from('.') {
		include 'src/java/**/*.java'
		include 'src/jastadd/**/*'
		include 'src/javacc/**/*'
		include 'src/res/**/*'
		include 'src/template/**/*'
		include 'tools/Markdown.pl'
		include 'tools/*.jar'
		include 'tinytemplate/src/**/*'
		include 'tinytemplate/LICENSE.txt'
		include 'tinytemplate/README.md'
		include 'doc/index.md.in'
		include 'doc/index.html'
		include 'doc/reference-manual.md'
		include 'doc/reference-manual.html'
		include 'doc/release-notes.md'
		include 'doc/release-notes.html'
		include 'LICENSE'
		include 'README.md'
		include 'README-binary-dist.md'
		include 'ChangeLog'
		include 'build.xml'
		include 'licenses/*'
		exclude '**/.git/**/*'
		exclude '**/.gitignore'
		exclude '**/.gitmodules'
	}
	into 'jastadd2-src'
}

task binaryDistReadme(type: Exec) {
	description 'Generate the README for binary distributions'
	commandLine 'perl', file('tools/Markdown.pl').path, 'README-binary-dist.md'

	inputs.file 'tools/Markdown.pl'
	inputs.file 'README-binary-dist.md'
	outputs.file 'README.html'

	doFirst {
		standardOutput = new FileOutputStream(file('README.html'))
	}
}

task binaryZip(type: Zip) {
	description 'build the binary Zip'
	dependsOn 'jar', 'documentation', 'binaryDistReadme'

	destinationDir = project.rootDir
	archiveName = 'jastadd2-bin.zip'

	from('.') {
		include 'README.html'
		include 'LICENSE'
		include 'ChangeLog'
		include 'jastadd2.jar'
		include 'doc/index.html'
		include 'doc/reference-manual.html'
		include 'doc/release-notes.html'
		include 'licenses/*'
	}
	into 'jastadd2-bin'
}

task documentation << {
	println 'Processing Markdown files'
	ant.copy(file: 'doc/index.md.in', toFile: 'doc/index.md', overwrite: 'true') {
		filterset {
			filter(token: 'VERSION', value: version)
		}
	}
	ant.chainedmapper(id: 'mdmapper') {
		flattenmapper()
		globmapper(from: '*.md', to: "doc${File.separator}*.html")
	}
	ant.apply(executable: 'perl', dest: 'doc') {
		arg(value: 'tools/Markdown.pl')
		srcfile()
		fileset(dir: 'doc', includes: '*.md')
		mapper(refid: 'mdmapper')
		redirector {
			outputmapper(refid: 'mdmapper')
		}
	}
}

task updateVersionString {
	description 'Store the current version string in src/gen-res/Version.properties'

	outputs.upToDateWhen {
		def props = new Properties()
		def output = file('src/gen-res/Version.properties')
		if (output.isFile()) {
			output.withInputStream { stream -> props.load(stream) }
		}
		props['version'] == project.version
	}

	doLast {
		file('src/gen-res').mkdirs()
		def date = new Date()
		Properties version = new Properties()
		version['version'] = project.version
		version['timestamp'] = date.format("yyyy-MM-dd'T'HH:mm'Z'")
		version['build.date'] = date.format('yyyy-MM-dd')
		version.store(file('src/gen-res/Version.properties').newWriter(), null)
	}
}

task grammarAst(type: JavaExec) {
	description 'build abstract syntax parser AST with JJTree'

	classpath = configurations.javacc
	main = 'org.javacc.jjtree.Main'
	args "-OUTPUT_DIRECTORY=${file('src/gen/org/jastadd/ast/AST').path}",
		'-NODE_PREFIX=""',
		file('src/javacc/ast/Ast.jjt').path


	inputs.file 'src/javacc/ast/Ast.jjt'
	outputs.dir 'src/gen/org/jastadd/ast/AST'

	doFirst {
		file('src/gen/org/jastadd/ast/AST').mkdirs()
	}
}

task aspectAst(type: JavaExec) {
	description 'build aspect parser AST with JJTree'

	classpath = configurations.javacc
	main = 'org.javacc.jjtree.Main'
	args "-OUTPUT_DIRECTORY=${file('src/gen/org/jastadd/jrag/AST').path}",
		file('src/javacc/jrag/Jrag.jjt').path


	inputs.file 'src/javacc/jrag/Jrag.jjt'
	outputs.dir 'src/gen/org/jastadd/jrag/AST'

	doFirst {
		file('src/gen/org/jastadd/jrag/AST').mkdirs()
	}
}

task grammarParser(type: JavaExec) {
	description 'build aspect and grammar parsers with JavaCC'
	dependsOn 'grammarAst'

	classpath = configurations.javacc
	main = 'org.javacc.parser.Main'
	args '-JDK_VERSION=1.4',
		"-OUTPUT_DIRECTORY=${file('src/gen/org/jastadd/ast/AST').path}",
		file('src/gen/org/jastadd/ast/AST/Ast.jj').path

	inputs.file 'src/gen/org/jastadd/ast/AST/Ast.jj'
	outputs.dir 'src/gen/org/jastadd/ast/AST'
}

task aspectParser(type: JavaExec) {
	description 'build aspect and grammar parsers with JavaCC'
	dependsOn 'aspectAst'

	classpath = configurations.javacc
	main = 'org.javacc.parser.Main'
	args '-JDK_VERSION=1.4',
		"-OUTPUT_DIRECTORY=${file('src/gen/org/jastadd/jrag/AST').path}",
		file('src/gen/org/jastadd/jrag/AST/Jrag.jj').path

	inputs.file 'src/gen/org/jastadd/jrag/AST/Jrag.jj'
	outputs.dir 'src/gen/org/jastadd/jrag/AST'
}

task additionalParserSources(type: Copy) {
	description 'copy additional parser sources'
	dependsOn 'grammarParser', 'aspectParser'

	from('src/javacc/jrag') {
		include '**/*.java'
	}
	into 'src/gen/org/jastadd/jrag'
}

compileJava.dependsOn 'generateJava'

task generateJava(type: JavaExec) {
	description 'Generate the Abstract Grammar sources from JastAdd aspects'
	dependsOn 'additionalParserSources'

	classpath = configurations.jastadd
	main = 'org.jastadd.JastAdd'
	args ([ '--rewrite=regular', '--grammar=Ast', '--jjtree',
		'--package=org.jastadd.ast.AST',
		'--lazyMaps=false', "--o=${file('src/gen').path}" ] +
		fileTree('src/jastadd')
			.include('**/*.ast')
			.include('**/*.jadd')
			.include('**/*.jrag')
			.files)

	inputs.dir 'src/jastadd'
	outputs.dir 'src/gen/org/jastadd/ast/AST'
}

clean.dependsOn 'cleanJar'
clean.dependsOn 'cleanGen'
clean.dependsOn 'cleanArtifacts'

task cleanGen(type: Delete) {
	description 'remove generated source files'
	delete 'src/gen/org'
	delete 'src/gen-res'
}

task cleanArtifacts(type: Delete) {
	description 'remove generated Zip and Jar files'
	delete 'jastadd2-src.zip'
	delete 'jastadd2-bin.zip'
	delete 'README.html'
	delete 'README-binary-dist.html' // Old README.html name.
}

task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadoc
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives javadocJar, sourcesJar
}

signing {
	sign configurations.archives
}

javadoc.options.addStringOption('Xdoclint:none', '-quiet')

sourceSets {
	main {
		java {
			srcDir 'src/gen'
			srcDir 'src/java'
			srcDir 'tinytemplate/src/java'
		}
		resources {
			srcDir 'src/gen-res'
		}
	}
}

uploadArchives {
	repositories.mavenDeployer {
		beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

		repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2') {
			if (project.hasProperty('ossrhUsername')) {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}
		}

		snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots') {
			if (project.hasProperty('ossrhUsername')) {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}
		}

		pom.project {
			name 'JastAdd2'
			packaging 'jar'
			description 'A metacompilation framework for Java using attribute grammars.'
			url 'http://jastadd.org'
			organization {
				name 'The JastAdd Team'
				url 'http://jastadd.org'
			}
			licenses {
				license {
					name 'Modified BSD License'
					url 'http://opensource.org/licenses/BSD-3-Clause'
					distribution 'repo'
				}
			}
			developers {
				developer {
					name 'Görel Hedin'
					email 'gorel.hedin@cs.lth.se'
				}
				developer {
					name 'Eva Magnusson'
				}
				developer {
					name 'Torbjörn Ekman'
				}
				developer {
					name 'Emma Söderberg'
					email 'emso@google.com'
				}
				developer {
					name 'Jesper Öqvist'
					email 'jesper.oqvist@cs.lth.se'
				}
				developer {
					name 'Niklas Fors'
					email 'niklas.fors@cs.lth.se'
				}
			}
			scm {
				connection 'scm:git:https://bitbucket.org/jastadd/jastadd2.git'
				url 'https://bitbucket.org/jastadd/jastadd2'
			}
		}
	}
}
